plugins {
    id 'java'
    id 'com.gradleup.shadow' version '9.3.1'
}

group = 'com.hytale.dualauth'

// Only change the version here
version = '1.1.4'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(25)
    }
}

processResources {
    inputs.property "version", project.version
    filesMatching("version.properties") {
        expand "version": project.version
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'net.bytebuddy:byte-buddy:1.18.4'
    implementation 'net.bytebuddy:byte-buddy-agent:1.18.4'
    implementation 'com.nimbusds:nimbus-jose-jwt:10.7'
    implementation 'com.google.crypto.tink:tink:1.20.0'
}

shadowJar {
    archiveClassifier.set('')
    archiveVersion.set('')
    archiveBaseName.set('dualauth-agent')
    manifest {
        attributes(
            'Main-Class': 'com.hytale.dualauth.agent.DualAuthAgent',
            'Premain-Class': 'com.hytale.dualauth.agent.DualAuthAgent',
            'Implementation-Version': project.version,
            'Can-Redefine-Classes': 'true',
            'Can-Retransform-Classes': 'true',
            'Boot-Class-Path': '.' 
        )
    }
    
    relocate 'net.bytebuddy', 'com.hytale.dualauth.libs.bytebuddy'

    exclude 'META-INF/*.SF'
    exclude 'META-INF/*.DSA'
    exclude 'META-INF/*.RSA'
    exclude 'META-INF/versions/**'
    
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}
