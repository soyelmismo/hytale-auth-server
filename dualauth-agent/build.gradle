plugins {
    id 'java'
    id 'com.gradleup.shadow' version '9.3.1'
}

group = 'ws.sanasol.dualauth'

// Only change the version here and in dualauth-agent/src/main/resources/manifest.json
version = '1.1.5'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(25)
    }
}

processResources {
    inputs.property "version", project.version
    filesMatching("version.properties") {
        expand "version": project.version
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'net.bytebuddy:byte-buddy:1.18.4'
    implementation 'net.bytebuddy:byte-buddy-agent:1.18.4'
    implementation 'com.nimbusds:nimbus-jose-jwt:10.7'
    implementation 'com.google.crypto.tink:tink:1.20.0'
}

shadowJar {
    archiveClassifier.set('')
    archiveVersion.set('')
    archiveBaseName.set('dualauth-agent')
    manifest {
        attributes(
            // Config for -javaagent mode
            'Main-Class': 'ws.sanasol.dualauth.agent.DualAuthAgent',
            'Premain-Class': 'ws.sanasol.dualauth.agent.DualAuthAgent',
            'Agent-Class': 'ws.sanasol.dualauth.agent.DualAuthAgent', // For dynamic attach
            'Can-Redefine-Classes': 'true',
            'Can-Retransform-Classes': 'true',
            'Implementation-Version': project.version
        )
    }
    
    relocate 'net.bytebuddy', 'ws.sanasol.dualauth.libs.bytebuddy'

    // CRITICAL: Exclude Hytale API stubs from final JAR
    // We only use these for compilation; at runtime Hytale provides the real classes
    exclude 'com/hypixel/hytale/server/core/plugin/**'

    exclude 'META-INF/*.SF'
    exclude 'META-INF/*.DSA'
    exclude 'META-INF/*.RSA'
    exclude 'META-INF/versions/**'
    
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}
